#!/usr/bin/env bash
# Maintainer: Hunter Peavey < printf "srht@die.bots" | sed "s/die.bots/krathalan.net/g" >
# shellcheck disable=SC2034
# shellcheck disable=SC2154

# General package information
pkgname=krathalans-scripts-git
pkgver=r438.e2221b6
pkgrel=1
pkgdesc="Krathalan's personal scripts"
url="https://github.com/krathalan/miscellaneous-scripts"
license=("GPL3")
arch=("any")

install=restart-service.install

# Dependencies
depends=("termdown" "jq" "bc" "curl" "git")
makedepends=("git")
optdepends=("ffmpeg: for audio_to_opus"
            "imagemagick: for make_gif.sh"
            "perl-image-exiftool: for scramble_photos.sh"
            "grim: for Wayland screenshots in prntscrn.sh"
            "slurp: for Wayland screenshot region selection in prntscrn.sh"
            "scrot: for Xorg screenshots in prntscrn.sh"
            "inotify-tools: for watch_add_packages{,.service}")

# Download information
source=("${pkgname}::git+${url}?signed")
validpgpkeys=("B46B326273E4A1D21AAA3F6F529AC10050BD24EF")
sha256sums=('SKIP')

# https://wiki.archlinux.org/index.php/VCS_PKGBUILD_Guidelines#Git
pkgver() {
  cd "${srcdir}/${pkgname}" || exit

  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

package() {
  cd "${srcdir}/${pkgname}" || exit

  for file in ./*; do
    if [[ ! -d "${file}" ]] && head -n1 "${file}" | grep -q "\#\!/.*sh"; then
      install -D -m755 "${file}" "${pkgdir}/usr/bin/${file}"
    elif [[ ! -d "${file}" ]] && [[ "${file}" == *".service" ]]; then
      install -D -m644 "${file}" "${pkgdir}/usr/lib/systemd/system/${file}"
    fi
  done
}
