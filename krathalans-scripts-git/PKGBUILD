#!/usr/bin/env bash
# Maintainer: Hunter Peavey < krathalan at disroot dot org >
# shellcheck disable=SC2034
# shellcheck disable=SC2154

# General package information
pkgname=krathalans-scripts-git
pkgver=r377.b6732a5
pkgrel=1
pkgdesc="Krathalan's personal scripts"
url="https://git.sr.ht/~krathalan/miscellaneous-scripts"
license=("GPL3")
arch=("any")

# Dependencies
depends=()
makedepends=("git")

# Download information
source=("${pkgname}::git+${url}")
sha256sums=('SKIP')

# https://wiki.archlinux.org/index.php/VCS_PKGBUILD_Guidelines#Git
pkgver() {
  cd "${srcdir}/${pkgname}" || exit

  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

package() {
  cd "${srcdir}/${pkgname}" || exit

  for file in ./*; do
    if [[ ! -d "${file}" ]] && head -n1 "${file}" | grep -q "\#\!/.*sh"; then
      install -D -m755 "${file}" "${pkgdir}/usr/bin/${file}"
    fi
  done
}
