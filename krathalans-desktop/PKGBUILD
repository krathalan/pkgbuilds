#!/usr/bin/env bash
# Maintainer: Hunter Peavey < printf "srht@die.bots" | sed "s/die.bots/krathalan.net/g" >
# shellcheck disable=SC2034
# shellcheck disable=SC2154

# General package information
pkgname=krathalans-desktop
pkgver=62
pkgrel=2
pkgdesc="Packages and configuration for Krathalan's desktop"
url="https://git.sr.ht/~krathalan/pkgbuilds"
license=("GPL3")
arch=("any")

# Overwrite config files that are owned by packages, e.g. /etc/pacman.conf
install=overwrite-configs.install

source=("https://git.sr.ht/~krathalan/etc-config/archive/${pkgver}.tar.gz" "https://git.sr.ht/~krathalan/etc-config/blob/master/signatures/${pkgver}.tar.gz.sig")
validpgpkeys=("B46B326273E4A1D21AAA3F6F529AC10050BD24EF")
sha512sums=("87ba4338f58a20ca52240499b5ad1ceb9bbc8bebd10dba3c1d9aa87a739d2f7d3d5f419f910a1f2da08c9c30b51d2584d9e6754aeb7af8b467b2d1274004a7fd" "22017381b840203a0a78a2747b5325e7c8e8504e2cd29399cdf7babbad147f902e2177a09024d255b8c84600bd8669a3fdeec065445282763b825a0141037633")

# Dependencies
depends=(
    # From builds.krathalan.net
    "krathalans-apparmor-profiles-git"
    "krathalans-meta" "krathalans-scripts-git"
    "krathalans-systemd-sandboxing-git"
    "polybar" "redshift-no-geoclue" "xob"

    # Also from builds.krathalan.net; for f-droid server
    "android-sdk" "android-sdk-build-tools" "fdroidserver"
    "python-paramiko"

    # Xorg packages
    "code" "dmenu" "dunst" "feh" "i3-gaps" "i3lock" "i3status"
    "picom" "rofi" "scrot" "xorg-server" "xorg-xinit"

    # System packages
    "amd-ucode"
)

package() {
    cd "${srcdir}/etc-config-${pkgver}/desktop" || exit

    install -D -m644 makepkg.conf "${pkgdir}/etc/krathalan/makepkg.conf"

    install -D -m644 dracut.conf.d/krathalans-custom-desktop.conf "${pkgdir}/etc/dracut.conf.d/krathalans-custom-desktop.conf"

    install -D -m644 systemd/system/i3lock.service "${pkgdir}/etc/systemd/system/i3lock.service"
}
