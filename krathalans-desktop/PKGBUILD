#!/usr/bin/env bash
# Maintainer: Hunter Peavey < printf "srht@die.bots" | sed "s/die.bots/krathalan.net/g" >
# shellcheck disable=SC2034
# shellcheck disable=SC2154

# General package information
pkgname=krathalans-desktop
pkgver=58
pkgrel=1
pkgdesc="Packages and configuration for Krathalan's desktop"
url="https://git.sr.ht/~krathalan/pkgbuilds"
license=("GPL3")
arch=("any")

# Overwrite config files that are owned by packages, e.g. /etc/pacman.conf
install=overwrite-configs.install

source=("https://git.sr.ht/~krathalan/etc-config/archive/${pkgver}.tar.gz" "https://git.sr.ht/~krathalan/etc-config/blob/master/signatures/${pkgver}.tar.gz.sig")
validpgpkeys=("B46B326273E4A1D21AAA3F6F529AC10050BD24EF")
sha512sums=("d8967aa051b763895cc401dd71bce55bc9300b08fc7aa09b250fc3a266e087f8ea863ca0d0e907aa848f892705941eb0d70ebef860ffe7948ce6fcb5b61f588d" "b0c4f1d58fe39e9345d0c5e1a0cabb50668a361428da2640ab1c725201500108058d42b50624ff2bc3f8e3bba06db5d3ecb7df8fd072c8d5cb1f5b8430119560")

# Dependencies
depends=(
    # From builds.krathalan.net
    "polybar" "redshift-no-geoclue" "xob"

    # Also from builds.krathalan.net; for f-droid server
    "android-sdk" "android-sdk-build-tools" "fdroidserver"
    "python-paramiko"

    # Xorg packages
    "dmenu" "dunst" "feh" "i3-gaps" "i3lock" "i3status"
    "picom" "rofi" "scrot" "xorg-server" "xorg-xinit"

    # System packages
    "amd-ucode"
)

package() {
    cd "${srcdir}/etc-config-${pkgver}/desktop" || exit

    install -D -m644 makepkg.conf "${pkgdir}/etc/krathalan/makepkg.conf"

    install -D -m644 dracut.conf.d/krathalans-custom-desktop.conf "${pkgdir}/etc/dracut.conf.d/krathalans-custom-desktop.conf"

    install -D -m644 systemd/system/i3lock.service "${pkgdir}/etc/systemd/system/i3lock.service"
}
