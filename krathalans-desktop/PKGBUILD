#!/usr/bin/env bash
# Maintainer: Hunter Peavey < printf "srht@die.bots" | sed "s/die.bots/krathalan.net/g" >
# shellcheck disable=SC2034
# shellcheck disable=SC2154

# General package information
pkgname=krathalans-desktop
pkgver=66
pkgrel=1
pkgdesc="Packages and configuration for Krathalan's desktop"
url="https://git.sr.ht/~krathalan/pkgbuilds"
license=("GPL3")
arch=("any")

# Overwrite config files that are owned by packages, e.g. /etc/pacman.conf
install=overwrite-configs.install

source=("https://git.sr.ht/~krathalan/etc-config/archive/${pkgver}.tar.gz" "https://git.sr.ht/~krathalan/etc-config/blob/master/signatures/${pkgver}.tar.gz.sig")
validpgpkeys=("B46B326273E4A1D21AAA3F6F529AC10050BD24EF")
sha512sums=("53dd616b47e64b5a751c29d51c1ca993c31ec6e45d0790493f9e4f6416fb2b8a60e228f88728b3cb097d1ea51e73fa96e1ca88fc950cbaa6e69013d72c4dd983" "5ab023b2a50be557c3a79c85cc03bac03b963b686f32ab23e299b1c520c8db7ed7c40d914f8044ba2bead3e2387944a69387170ac14fb293739ceafee7c35404")

# Dependencies
depends=(
    # From builds.krathalan.net
    "krathalans-apparmor-profiles-git"
    "krathalans-meta" "krathalans-scripts-git"
    "krathalans-systemd-sandboxing-git"
    "polybar" "redshift-no-geoclue" "xob"

    # Also from builds.krathalan.net; for f-droid server
    "android-sdk" "android-sdk-build-tools" "fdroidserver"
    "python-paramiko"

    # Xorg packages
    "code" "dmenu" "dunst" "feh" "i3-gaps" "i3lock" "i3status"
    "picom" "rofi" "scrot" "xorg-server" "xorg-xinit"

    # System packages
    "amd-ucode"
)

package() {
    cd "${srcdir}/etc-config-${pkgver}/desktop" || exit

    install -D -m644 makepkg.conf "${pkgdir}/etc/krathalan/makepkg.conf"

    install -D -m644 dracut.conf.d/krathalans-custom-desktop.conf "${pkgdir}/etc/dracut.conf.d/krathalans-custom-desktop.conf"

    install -D -m644 systemd/system/i3lock.service "${pkgdir}/etc/systemd/system/i3lock.service"
}
