#!/usr/bin/env bash
# Maintainer: Hunter Peavey < printf "srht@die.bots" | sed "s/die.bots/krathalan.net/g" >
# shellcheck disable=SC2034
# shellcheck disable=SC2154

# General package information
pkgname=krathalans-desktop
pkgver=73
pkgrel=1
pkgdesc="Packages and configuration for Krathalan's desktop"
url="https://git.sr.ht/~krathalan/pkgbuilds"
license=("GPL3")
arch=("any")

# Overwrite config files that are owned by packages, e.g. /etc/pacman.conf
install=overwrite-configs.install

source=("https://git.sr.ht/~krathalan/etc-config/archive/${pkgver}.tar.gz" "https://git.sr.ht/~krathalan/etc-config/blob/master/signatures/${pkgver}.tar.gz.sig")
validpgpkeys=("B46B326273E4A1D21AAA3F6F529AC10050BD24EF")
sha512sums=("74e4bc0b53eff1f531269b3167af46d842144101a8d95431c488f23f78169220741bc1f301cc2881020bb30f394ba921a199096b395c0d6768136b931bedcece" "fa282afa0223f39667d1f26e56607c03557988395aed5ecd4b64a81d14d9bd36e8f47d49bce790cae8e44d020fafd24591c06f849ff13742939c951ee026a88f")

# Dependencies
depends=(
    # From builds.krathalan.net
    "krathalans-apparmor-profiles-git"
    "krathalans-meta" "krathalans-scripts-git"
    "krathalans-systemd-sandboxing-git"
    "polybar" "redshift-no-geoclue" "xob"

    # Xorg packages
    "code" "dmenu" "dunst" "feh" "i3-gaps" "i3lock" "i3status"
    "picom" "rofi" "scrot" "xorg-server" "xorg-xinit"

    # System packages
    "amd-ucode"
)

package() {
    cd "${srcdir}/etc-config-${pkgver}/desktop" || exit

    install -D -m644 makepkg.conf "${pkgdir}/etc/krathalan/makepkg.conf"
    install -D -m644 mkinitcpio.conf "${pkgdir}/etc/krathalan/mkinitcpio.conf"

    install -D -m644 systemd/system/i3lock.service "${pkgdir}/etc/systemd/system/i3lock.service"

    install -D -m644 vconsole.conf "${pkgdir}/etc/vconsole.conf"
}
