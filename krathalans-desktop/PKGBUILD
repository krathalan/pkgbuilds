#!/usr/bin/env bash
# Maintainer: Hunter Peavey < printf "srht@die.bots" | sed "s/die.bots/krathalan.net/g" >
# shellcheck disable=SC2034
# shellcheck disable=SC2154

# General package information
pkgname=krathalans-desktop
pkgver=69
pkgrel=2
pkgdesc="Packages and configuration for Krathalan's desktop"
url="https://git.sr.ht/~krathalan/pkgbuilds"
license=("GPL3")
arch=("any")

# Overwrite config files that are owned by packages, e.g. /etc/pacman.conf
install=overwrite-configs.install

source=("https://git.sr.ht/~krathalan/etc-config/archive/${pkgver}.tar.gz" "https://git.sr.ht/~krathalan/etc-config/blob/master/signatures/${pkgver}.tar.gz.sig")
validpgpkeys=("B46B326273E4A1D21AAA3F6F529AC10050BD24EF")
sha512sums=("c991c07cbae8bf5d05776c7ba8521e27f76f8aceabc7a568af3c0d2124793f8e245e4a52aa560d2fde9b415f0caff6b74afc2b97e77d4faab97407f1e06fc3ce" "7bebc9457f942f352ef32ad410f9b3c6981e90e1c8370e3db477f00e687e273dd26cf96bdd142a395b4dca9d5457b044f7c6114d1b2a95e06fce774330f24832")

# Dependencies
depends=(
    # From builds.krathalan.net
    "krathalans-apparmor-profiles-git"
    "krathalans-meta" "krathalans-scripts-git"
    "krathalans-systemd-sandboxing-git"
    "polybar" "redshift-no-geoclue" "xob"

    # Xorg packages
    "code" "dmenu" "dunst" "feh" "i3-gaps" "i3lock" "i3status"
    "picom" "rofi" "scrot" "xorg-server" "xorg-xinit"

    # System packages
    "amd-ucode"
)

package() {
    cd "${srcdir}/etc-config-${pkgver}/desktop" || exit

    install -D -m644 makepkg.conf "${pkgdir}/etc/krathalan/makepkg.conf"

    install -D -m644 systemd/system/i3lock.service "${pkgdir}/etc/systemd/system/i3lock.service"
}
