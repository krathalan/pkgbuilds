#!/usr/bin/env bash
# Maintainer: Hunter Peavey < printf "srht@die.bots" | sed "s/die.bots/krathalan.net/g" >
# shellcheck disable=SC2034
# shellcheck disable=SC2154

# General package information
pkgname=krathalans-desktop
pkgver=56
pkgrel=1
pkgdesc="Packages and configuration for Krathalan's desktop"
url="https://git.sr.ht/~krathalan/pkgbuilds"
license=("GPL3")
arch=("any")

# Overwrite config files that are owned by packages, e.g. /etc/pacman.conf
install=overwrite-configs.install

source=("https://git.sr.ht/~krathalan/etc-config/archive/${pkgver}.tar.gz" "https://git.sr.ht/~krathalan/etc-config/blob/master/signatures/${pkgver}.tar.gz.sig")
validpgpkeys=("B46B326273E4A1D21AAA3F6F529AC10050BD24EF")
sha512sums=("adc6cb9261e90116dd586716727566e5876e4ce2cde095f2e9ffd5a6dfed5d5172f96b976bb613ece0732c2d09731bcf106f4fe752777d68de44839a5fb7278c" "8c9f9cd7fd1a1f2f88f6491e307ffb9b5b37b8b6abcf9ef3c8e2754dc8b24aad00909d94aee4d319cd456d0ebc2e8de5b2bbea0e1f77a1727f409f3ebe4a9d4c")

# Dependencies
depends=(
    # From builds.krathalan.net
    "polybar" "redshift-no-geoclue" "xob"

    # Also from builds.krathalan.net; for f-droid server
    "android-sdk" "android-sdk-build-tools" "fdroidserver"
    "python-paramiko"

    # Xorg packages
    "dmenu" "dunst" "feh" "i3-gaps" "i3lock" "i3status"
    "picom" "rofi" "scrot" "xorg-server" "xorg-xinit"

    # System packages
    "amd-ucode"
)

package() {
    cd "${srcdir}/etc-config-${pkgver}/desktop" || exit

    install -D -m644 makepkg.conf "${pkgdir}/etc/krathalan/makepkg.conf"

    install -D -m644 dracut.conf.d/krathalans-custom-desktop.conf "${pkgdir}/etc/dracut.conf.d/krathalans-custom-desktop.conf"

    install -D -m644 systemd/system/i3lock.service "${pkgdir}/etc/systemd/system/i3lock.service"
}
