# AUR (original) PKGBUILD author/maintainer: spikecodes <19519553+spikecodes@users.noreply.github.com>
# Updated by Krathalan to follow Arch Rust packaging guidelines as of Nov. 2022 and adds a check() function
pkgname=libreddit-git
_pkgname=${pkgname%-git}
pkgver=r863.7efa26e
pkgrel=1
pkgdesc="Alternative private front-end to Reddit (Krathalan's PKGBUILD)"
arch=('x86_64')
url="https://github.com/spikecodes/libreddit"
license=("AGPL-3.0")
makedepends=("git" "cargo")
provides=(${_pkgname})
conflicts=(${_pkgname})
source=("${_pkgname}::git+${url}")
sha256sums=("SKIP")

pkgver() {
	cd "${_pkgname}"
	( set -o pipefail
		printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	)
}

prepare() {
  cd "${_pkgname}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "${_pkgname}"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd "${_pkgname}"
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  cd "${_pkgname}"
	install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"
	install -Dm0755 -t "${pkgdir}/usr/bin/" "target/release/${_pkgname}"
}
